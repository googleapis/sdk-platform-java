load("@rules_java//java:defs.bzl", "java_proto_library", "java_test")
load("//:rules_bazel/unit_golden_update.bzl", "golden_update")

package(default_visibility = ["//visibility:public"])

UPDATE_GOLDENS_TESTS = [
    "BatchingDescriptorComposerTest",
]

TESTS = UPDATE_GOLDENS_TESTS + [
    "RetrySettingsComposerTest",
]

TEST_DEPS = [
    ":common_resources_java_proto",
    "//:service_config_java_proto",
    "//src/main/java/com/google/api/generator/engine/ast",
    "//src/main/java/com/google/api/generator/engine/writer",
    "//src/main/java/com/google/api/generator/gapic/composer",
    "//src/main/java/com/google/api/generator/gapic/composer/common",
    "//src/main/java/com/google/api/generator/gapic/composer/grpc",
    "//src/test/java/com/google/api/generator/test/framework:asserts",
    "//src/test/java/com/google/api/generator/test/framework:utils",
    "//src/main/java/com/google/api/generator/gapic/composer/samplecode",
    "//src/test/java/com/google/api/generator/testutils",
    "//src/main/java/com/google/api/generator/gapic/model",
    "//src/main/java/com/google/api/generator/gapic/protoparser",
    "//src/main/java/com/google/api/generator/gapic/composer/defaultvalue",
    "//src/test/java/com/google/api/generator/gapic/testdata:deprecated_service_java_proto",
    "//src/test/java/com/google/api/generator/gapic/testdata:bookshop_java_proto",
    "//src/test/java/com/google/api/generator/gapic/testdata:showcase_java_proto",
    "//src/test/java/com/google/api/generator/gapic/testdata:testgapic_java_proto",
    "@com_google_api_api_common//jar",
    "@com_google_api_gax_java//gax",
    "@com_google_api_api_common",
    "@com_google_googleapis//google/logging/v2:logging_java_proto",
    "@com_google_googleapis//google/pubsub/v1:pubsub_java_proto",
    "@com_google_googleapis//google/rpc:rpc_java_proto",
    "@com_google_guava_guava",
    "@com_google_protobuf//:protobuf_java",
    "@com_google_truth_truth//jar",
    "@junit_junit//jar",
]

java_library(
    name = "common",
    srcs = ["TestProtoLoader.java"],
    deps = TEST_DEPS,
)

filegroup(
    name = "common_files",
    srcs = glob(["*.java"]),
)

java_proto_library(
    name = "pubsub_java_proto",
    deps = [
        "@com_google_googleapis//google/pubsub/v1:pubsub_proto",
    ],
)

java_proto_library(
    name = "common_resources_java_proto",
    deps = [
        "@com_google_googleapis//google/cloud:common_resources_proto",
    ],
)

[java_test(
    name = test_name,
    srcs = [
        "%s.java" % test_name,
    ],
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/common/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.common.%s" % test_name,
    deps = TEST_DEPS + [":common"],
) for test_name in TESTS]

# Run `bazel run src/test/java/com/google/api/generator/gapic/composer/common:testTargetName_update`
# to update goldens as expected generated code.
# `ServiceClient*` tests are not supported now since they are still in active development.

[golden_update(
    name = "%s_update" % test_name,
    srcs = [
        "%s.java" % test_name,
    ],
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/common/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.common.%s" % test_name,
    deps = TEST_DEPS + [":common"],
) for test_name in UPDATE_GOLDENS_TESTS]
