load("@rules_java//java:defs.bzl", "java_proto_library", "java_test")

TESTS = [
    "GrpcServiceCallableFactoryClassComposerTest",
    "GrpcServiceStubClassComposerTest",
    "MockServiceClassComposerTest",
    "MockServiceImplClassComposerTest",
    "ServiceClientClassComposerTest",
    "ServiceClientTestClassComposerTest",
    "ServiceSettingsClassComposerTest",
    "ServiceStubClassComposerTest",
    "ServiceStubSettingsClassComposerTest",
]

COMMON_SRCS = [
    "GrpcTestProtoLoader.java",
]

TEST_DEPS = [
    ":common_resources_java_proto",
    "//:service_config_java_proto",
    "//src/main/java/com/google/api/generator/engine/ast",
    "//src/main/java/com/google/api/generator/engine/writer",
    "//src/main/java/com/google/api/generator/gapic/composer",
    "//src/main/java/com/google/api/generator/gapic/composer/comment",
    "//src/main/java/com/google/api/generator/gapic/composer/common",
    "//src/main/java/com/google/api/generator/gapic/utils",
    "//src/main/java/com/google/api/generator/gapic/composer/grpc",
    "//src/test/java/com/google/api/generator/gapic/composer/common",
    "//src/test/java/com/google/api/generator/test/framework:asserts",
    "//src/test/java/com/google/api/generator/test/framework:utils",
    "//src/test/java/com/google/api/generator/testutils",
    "//src/main/java/com/google/api/generator/gapic/composer/samplecode",
    "//src/main/java/com/google/api/generator/gapic/model",
    "//src/main/java/com/google/api/generator/gapic/protoparser",
    "//src/main/java/com/google/api/generator/gapic/composer/defaultvalue",
    "//src/test/java/com/google/api/generator/gapic/testdata:showcase_java_proto",
    "//src/test/java/com/google/api/generator/gapic/testdata:explicit_dynamic_routing_headers_testing_java_proto",
    "@com_google_api_api_common//jar",
    "@com_google_api_gax_java//gax",
    "@com_google_api_api_common",
    "@com_google_googleapis//google/logging/v2:logging_java_proto",
    "@com_google_googleapis//google/pubsub/v1:pubsub_java_proto",
    "@com_google_googleapis//google/rpc:rpc_java_proto",
    "@com_google_guava_guava",
    "@com_google_protobuf//:protobuf_java",
    "@com_google_truth_truth//jar",
    "@junit_junit//jar",
]

java_proto_library(
    name = "common_resources_java_proto",
    deps = [
        "@com_google_googleapis//google/cloud:common_resources_proto",
    ],
)

[java_test(
    name = test_name,
    size = "medium",
    srcs = [
        "%s.java" % test_name,
    ] + COMMON_SRCS,
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/grpc/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/composer/grpc/goldens/samples:golden_samples_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.grpc.%s" % test_name,
    deps = TEST_DEPS,
) for test_name in TESTS]

# Run `bazel run src/test/java/com/google/api/generator/gapic/composer/grpc:testTargetName_update`
# to update goldens as expected generated code.
# `ServiceClient*` tests are not supported now since they are still in active development.

[java_binary(
    name = "%s_update" % test_name,
    srcs = ["%s.java" % test_name] + COMMON_SRCS,
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/grpc/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/composer/grpc/goldens/samples:golden_samples_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    main_class = "com.google.api.generator.test.framework.SingleJUnitTestRunner",
    args = ["com.google.api.generator.gapic.composer.grpc.%s" % test_name],
    deps = TEST_DEPS,
    runtime_deps = ["//src/test/java/com/google/api/generator/test/framework:junit_runner"],
) for test_name in TESTS]
