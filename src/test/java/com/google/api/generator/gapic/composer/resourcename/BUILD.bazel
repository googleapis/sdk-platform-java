load("@rules_java//java:defs.bzl", "java_proto_library", "java_test")
load("//:rules_bazel/java/java_diff_test.bzl", "golden_update")

package(default_visibility = ["//visibility:public"])

UPDATE_GOLDENS_TESTS = [
    "ResourceNameHelperClassComposerTest",
]

TESTS = UPDATE_GOLDENS_TESTS + [
    "ResourceNameTokenizerTest",
]

filegroup(
    name = "resourcename_files",
    srcs = ["{0}.java".format(f) for f in TESTS],
)

TEST_DEPS = [
    ":common_resources_java_proto",
    "//src/main/java/com/google/api/generator/engine/ast",
    "//src/main/java/com/google/api/generator/engine/writer",
    "//src/main/java/com/google/api/generator/gapic/composer/resourcename",
    "//src/main/java/com/google/api/generator/gapic/model",
    "//src/main/java/com/google/api/generator/gapic/protoparser",
    "//src/test/java/com/google/api/generator/gapic/composer/common",
    "//src/test/java/com/google/api/generator/gapic/testdata:showcase_java_proto",
    "//src/test/java/com/google/api/generator/test/framework:asserts",
    "//src/test/java/com/google/api/generator/test/framework:utils",
    "//src/test/java/com/google/api/generator/testutils",
    "@com_google_api_api_common//jar",
    "@com_google_api_gax_java//gax",
    "@com_google_googleapis//google/logging/v2:logging_java_proto",
    "@com_google_googleapis//google/rpc:rpc_java_proto",
    "@com_google_guava_guava",
    "@com_google_protobuf//:protobuf_java",
    "@com_google_truth_truth//jar",
    "@junit_junit//jar",
]

[java_test(
    name = test_name,
    srcs = ["{0}.java".format(test_name)],
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/resourcename/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.resourcename.{0}".format(test_name),
    deps = TEST_DEPS,
) for test_name in TESTS]

java_proto_library(
    name = "common_resources_java_proto",
    deps = [
        "@com_google_googleapis//google/cloud:common_resources_proto",
    ],
)

# Run `bazel run src/test/java/com/google/api/generator/gapic/composer/resourcename:testTargetName_update`
# to update goldens as expected generated code.
# `ServiceClient*` tests are not supported now since they are still in active development.

[golden_update(
    name = "{0}_update".format(test_name),
    srcs = [
        "{0}.java".format(test_name),
    ],
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/resourcename/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.resourcename.{0}".format(test_name),
    deps = TEST_DEPS,
) for test_name in UPDATE_GOLDENS_TESTS]
