load(
    "@com_google_googleapis_imports//:imports.bzl",
    "java_gapic_assembly_gradle_pkg",
    "java_gapic_library",
    "java_gapic_test",
    "java_grpc_library",
    "java_proto_library",
)
load(
    "//:rules_bazel/java/integration_test.bzl",
    "golden_update",
    "integration_test",
)

package(default_visibility = ["//visibility:public"])

####################################################
# Integration Test Rules
####################################################

INTEGRATION_TEST_LIBRARIES = [
    "asset",  # Basic case.
    "credentials",  # Check that the capital name edge case is handled.
    "kms",  # Mixins, with an override in the proto file.
    "logging",  # Java package remapping in gapic.yaml.
    "redis",  # Has a gapic.yaml.
    "library",  # No gRPC service config.
]

# Keys must match the values in INTEGRATION_TEST_LIBRARIES above.
API_GAPIC_TARGETS = {
    "asset": "@com_google_googleapis//google/cloud/asset/v1:asset_java_gapic",
    "credentials": "@com_google_googleapis//google/iam/credentials/v1:credentials_java_gapic",
    "kms": ":kms_java_gapic",  # Local target because mixins are not rolled out yet.
    "logging": "@com_google_googleapis//google/logging/v2:logging_java_gapic",
    "redis": "@com_google_googleapis//google/cloud/redis/v1beta1:redis_java_gapic",
    "library": "@com_google_googleapis//google/example/library/v1:library_java_gapic",
}

[integration_test(
    name = lib_name,
    data = ["//test/integration/goldens/%s:goldens_files" % lib_name],
    target = API_GAPIC_TARGETS[lib_name],
) for lib_name in INTEGRATION_TEST_LIBRARIES]

[golden_update(
    name = "%s_update" % lib_name,
    data = ["//test/integration/goldens/%s:goldens_files" % lib_name],
    target = API_GAPIC_TARGETS[lib_name],
) for lib_name in INTEGRATION_TEST_LIBRARIES]

####################################################
# API Library Rules
####################################################
# These will eventually go away once more APIs in googleapis have been migrated to the
# microgenerator.

# Asset API.
java_gapic_test(
    name = "asset_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.asset.v1.AssetServiceClientTest",
    ],
    runtime_deps = ["@com_google_googleapis//google/cloud/asset/v1:asset_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-asset-v1-java",
    deps = [
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_gapic",
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_grpc",
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_proto",
        "@com_google_googleapis//google/cloud/asset/v1:asset_proto",
    ],
)

# Redis API.
java_gapic_test(
    name = "redis_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.redis.v1beta1.CloudRedisClientTest",
    ],
    runtime_deps = ["@com_google_googleapis//google/cloud/redis/v1beta1:redis_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-redis-v1beta1-java",
    deps = [
        "@com_google_googleapis//google/cloud/redis/v1beta1:redis_java_gapic",
        "@com_google_googleapis//google/cloud/redis/v1beta1:redis_java_grpc",
        "@com_google_googleapis//google/cloud/redis/v1beta1:redis_java_proto",
        "@com_google_googleapis//google/cloud/redis/v1beta1:redis_proto",
    ],
)

# Logging API
java_gapic_test(
    name = "logging_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.logging.v2.ConfigClientTest",
        "com.google.cloud.logging.v2.LoggingClientTest",
        "com.google.cloud.logging.v2.MetricsClientTest",
    ],
    runtime_deps = ["@com_google_googleapis//google/logging/v2:logging_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-logging-v2-java",
    deps = [
        "@com_google_googleapis//google/logging/v2:logging_java_gapic",
        "@com_google_googleapis//google/logging/v2:logging_java_grpc",
        "@com_google_googleapis//google/logging/v2:logging_java_proto",
        "@com_google_googleapis//google/logging/v2:logging_proto",
    ],
)

# example/library API.
# Tests the edge case of a legitimately missing gRPC service config file.
java_gapic_test(
    name = "library_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.example.library.v1.LibraryServiceClientTest",
    ],
    runtime_deps = ["@com_google_googleapis//google/example/library/v1:library_java_gapic_test"],
)

# Open Source Packages
java_gapic_assembly_gradle_pkg(
    name = "google-cloud-example-library-v1-java",
    deps = [
        "@com_google_googleapis//google/example/library/v1:library_java_gapic",
        "@com_google_googleapis//google/example/library/v1:library_java_grpc",
        "@com_google_googleapis//google/example/library/v1:library_java_proto",
        "@com_google_googleapis//google/example/library/v1:library_proto",
    ],
)

# IAMCredentials.
# Check that the capital name edge case is handled.
java_gapic_test(
    name = "credentials_java_gapic_test_suite",
    test_classes = [
        # Match google3.
        "com.google.cloud.iam.credentials.v1.IAMCredentialsClientTest",
    ],
    runtime_deps = ["@com_google_googleapis//google/iam/credentials/v1:credentials_java_gapic_test"],
)

# Open Source Packages
java_gapic_assembly_gradle_pkg(
    name = "google-cloud-iam-credentials-v1-java",
    deps = [
        "@com_google_googleapis//google/iam/credentials/v1:credentials_java_gapic",
        "@com_google_googleapis//google/iam/credentials/v1:credentials_java_grpc",
        "@com_google_googleapis//google/iam/credentials/v1:credentials_java_proto",
        "@com_google_googleapis//google/iam/credentials/v1:credentials_proto",
    ],
)

# KMS (for mixins).
load("@rules_proto//proto:defs.bzl", "proto_library")
load("@com_google_googleapis_imports//:imports.bzl", "proto_library_with_info")

proto_library(
    name = "kms_proto",
    srcs = [
        "apis/kms/v1/resources.proto",
        "apis/kms/v1/service.proto",
    ],
    deps = [
        "@com_google_googleapis//google/api:annotations_proto",
        "@com_google_googleapis//google/api:client_proto",
        "@com_google_googleapis//google/api:field_behavior_proto",
        "@com_google_googleapis//google/api:resource_proto",
        "@com_google_googleapis//google/iam/v1:iam_policy_proto",
        "@com_google_googleapis//google/iam/v1:policy_proto",
        "@com_google_protobuf//:duration_proto",
        "@com_google_protobuf//:field_mask_proto",
        "@com_google_protobuf//:struct_proto",
        "@com_google_protobuf//:timestamp_proto",
        "@com_google_protobuf//:wrappers_proto",
    ],
)

proto_library_with_info(
    name = "kms_proto_with_info",
    deps = [
        ":kms_proto",
        "@com_google_googleapis//google/cloud:common_resources_proto",
        "@com_google_googleapis//google/iam/v1:iam_policy_proto",
        "@com_google_googleapis//google/iam/v1:policy_proto",
    ],
)

java_proto_library(
    name = "kms_java_proto",
    deps = [":kms_proto"],
)

java_grpc_library(
    name = "kms_java_grpc",
    srcs = [":kms_proto"],
    deps = [":kms_java_proto"],
)

java_gapic_library(
    name = "kms_java_gapic",
    srcs = [":kms_proto_with_info"],
    grpc_service_config = "@com_google_googleapis//google/cloud/kms/v1:cloudkms_grpc_service_config.json",
    # For the IAM mixin.
    service_yaml = "apis/kms/v1/cloudkms_test_mixins_v1.yaml",
    test_deps = [
        ":kms_java_grpc",
        "@com_google_googleapis//google/iam/v1:iam_java_grpc",
    ],
    deps = [
        ":kms_java_proto",
        "@com_google_googleapis//google/iam/v1:iam_java_proto",
    ],
)

java_gapic_test(
    name = "kms_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.kms.v1.KeyManagementServiceClientTest",
    ],
    runtime_deps = [":kms_java_gapic_test"],
)

# Open Source Packages
java_gapic_assembly_gradle_pkg(
    name = "google-cloud-kms-v1-java",
    deps = [
        ":kms_java_gapic",
        "@com_google_googleapis//google/cloud/kms/v1:kms_java_grpc",
        "@com_google_googleapis//google/cloud/kms/v1:kms_java_proto",
        "@com_google_googleapis//google/cloud/kms/v1:kms_proto",
    ],
)
