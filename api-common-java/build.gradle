import groovy.io.FileType

buildscript {
  repositories {
    mavenLocal()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8",
      "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'com.github.sherter.google-java-format'
apply plugin: 'io.codearte.nexus-staging'

group = "com.google.api"
archivesBaseName = "api-common"

project.version = new File("version.txt").text.trim()

sourceCompatibility = 1.7
targetCompatibility = 1.7

// Dependencies
// ------------

ext {
  // Shortcuts for libraries we are using
  libraries = [
      javax_annotations: 'javax.annotation:javax.annotation-api:1.3.2',
      auto_value_annotations: 'com.google.auto.value:auto-value-annotations:1.7',
      auto_value: 'com.google.auto.value:auto-value:1.7',
      guava: 'com.google.guava:guava:29.0-android',
      jsr305: 'com.google.code.findbugs:jsr305:3.0.2',
      error_prone_annotations: 'com.google.errorprone:error_prone_annotations:2.3.4',

      // Testing
      junit: 'junit:junit:4.13',
      mockito: 'org.mockito:mockito-core:1.10.19',
      truth: 'com.google.truth:truth:1.0.1',
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  annotationProcessor libraries.auto_value

  compile libraries.guava,
    libraries.jsr305,
    libraries.javax_annotations,
    libraries.auto_value_annotations

  compileOnly libraries.error_prone_annotations

  testCompile libraries.junit,
    libraries.mockito,
    libraries.truth
}

clean.doFirst {
  delete 'tmp_gh-pages/'
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}

check.dependsOn jacocoTestReport

// jar with automatic module name:
jar {
  manifest {
    attributes('Automatic-Module-Name': 'com.google.api.apicommon')
  }
}

// Source jar
// ----------

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

// JavaDoc
// -------

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

javadoc.options {
  encoding = 'UTF-8'
  links 'https://docs.oracle.com/javase/7/docs/api/'
}

// Test Logging
// ------------

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    exceptionFormat = 'full'
  }
}


// Eclipse Annotation Processing
// -----------------------------

ext {
  eclipseAptFolder = '.apt_generated'
  eclipseSettingsDir = file('.settings')
}

configurations {
  codeGeneration
}

dependencies {
  codeGeneration libraries.auto_value, libraries.jsr305
  compile libraries.jsr305, libraries.auto_value_annotations
}

compileJava.classpath += configurations.codeGeneration

eclipse {
  jdt.file.withProperties {
    it['org.eclipse.jdt.core.compiler.processAnnotations'] = 'enabled'
  }
}

tasks.eclipseJdt {
  doFirst {
    def aptPrefs =
        file("${eclipseSettingsDir}/org.eclipse.jdt.apt.core.prefs")
    aptPrefs.parentFile.mkdirs()

    aptPrefs.text = """\
        eclipse.preferences.version=1
        org.eclipse.jdt.apt.aptEnabled=true
        org.eclipse.jdt.apt.genSrcDir=${eclipseAptFolder}
        org.eclipse.jdt.apt.reconcileEnabled=true
        """.stripIndent()

    file('.factorypath').withWriter {
      new groovy.xml.MarkupBuilder(it).'factorypath' {
        project.configurations.codeGeneration.each { dep->
          factorypathentry(
            kind:'EXTJAR',
            id:dep.absolutePath,
            enabled:true,
            runInBatchMode:false)
        }
      }
    }
  }
}

tasks.cleanEclipseJdt {
  doFirst {
    delete file("${eclipseSettingsDir}/org.eclipse.jdt.apt.core.prefs"),
      file('.factorypath')
  }
}

// Publishing
// ----------

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  if (project.hasProperty('signing.gnupg.executable')) {
    useGpgCmd()
  }
  sign configurations.archives
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'API Common'
          packaging 'jar'
          artifactId 'api-common'
          description 'Common utilities for Google APIs in Java'
          url 'https://github.com/googleapis/api-common-java'

          scm {
            url 'https://github.com/googleapis/api-common-java'
            connection 'scm:git:https://github.com/googleapis/api-common-java.git'
          }

          licenses {
            license {
              name 'BSD'
              url 'https://github.com/googleapis/api-common-java/blob/master/LICENSE'
            }
          }

          developers {
            developer {
              id 'GoogleAPIs'
              name 'GoogleAPIs'
              email 'googleapis@googlegroups.com'
              url 'https://github.com/googleapis'
              organization = 'Google, Inc.'
              organizationUrl 'https://www.google.com'
            }
          }
        }
      }
    }
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

// Formatting tasks
// ================

task verifyLicense {
  doLast {
    def licenseText = new File(rootProject.rootDir, 'license-header-javadoc.txt').text
    def srcFiles = []
    sourceSets
        .collectMany{it.allJava.getSrcDirs()}
        .each{it.eachFileRecurse(FileType.FILES, {srcFiles << new Tuple(it, it.text)})}
    srcFiles = srcFiles
        .findAll{it.get(0).path.endsWith(".java")}
        .collect{new Tuple(it.get(0), it.get(1).replaceAll("Copyright 20[0-9]{2}", "Copyright 20xx"))}
        .findAll{!it.get(1).startsWith(licenseText)}
    if (srcFiles.asList().size() > 0) {
      srcFiles.each({println 'missing license: ' + it.get(0)})
      throw new IllegalStateException("Above files do not have licenses")
    }
  }
}
test.dependsOn verifyLicense

googleJavaFormat {
  toolVersion '1.0'
}
tasks.googleJavaFormat {
  exclude '.apt_generated/**'
  exclude 'bin/**'
  exclude 'build/**'
}
tasks.verifyGoogleJavaFormat {
  exclude '.apt_generated/**'
  exclude 'bin/**'
  exclude 'build/**'
}
test.dependsOn verifyGoogleJavaFormat

// Release
// =======

task checkOutGhPages {
  doLast {
    if (!new File('tmp_gh-pages').exists()) {
      exec {
        commandLine 'git', 'clone', '--branch', 'gh-pages',
            '--single-branch', 'https://github.com/googleapis/api-common-java/', 'tmp_gh-pages'
      }
    }
  }
}

task copyFilesToGhPages {
  dependsOn 'checkOutGhPages'
  dependsOn 'javadoc'
  doLast {
    def newSiteDirPath = 'tmp_gh-pages/' + project.version + '/apidocs/'
    new File(newSiteDirPath).mkdirs()
    copy {
      from 'build/docs/javadoc'
      into newSiteDirPath
    }
    copy {
      from 'README.md'
      into 'tmp_gh-pages'
      rename { filename -> filename.replace 'README', 'index' }
    }
  }
}

task createApiDocsRedirect {
  dependsOn 'copyFilesToGhPages'
  doLast {
    def template = new File('templates/apidocs_index.html.template').text
    def outputContent = template.replace('{{siteVersion}}', project.version)
    new File('tmp_gh-pages/apidocs/index.html').write(outputContent)
  }
}

task publishDocs {
  dependsOn 'closeAndReleaseRepository'
  doLast {
    exec {
      workingDir './tmp_gh-pages'
      commandLine 'git', 'add', '.'
    }
    exec {
      workingDir './tmp_gh-pages'
      commandLine 'git', 'commit', '-m', 'Release docs for ' + project.version
    }
    exec {
      workingDir './tmp_gh-pages'
      commandLine 'git', 'push'
    }
  }
}

// 1. Regenerates the gh-pages branch under tmp_gh-pages
// 2. Stages the artifact on Sonatype
task stageRelease {
  dependsOn 'createApiDocsRedirect'
  doLast {
    exec {
      // We need to spawn a new gradle build process in order to upload appropriately
      // More details: http://stackoverflow.com/questions/31614735/gradle-uploadarchives-artificats-namespace-when-depending-on-a-plugin
      commandLine './gradlew', 'uploadArchives'
    }
  }
}

// 1. Closes and releases the artifact on Sonatype
// 2. Commits and pushes the new docs
// 3. Removes tmp_gh-pages
// Note: This task assumes that the 'stageRelease' task has been completed.
task finalizeRelease {
  dependsOn 'publishDocs'
  doLast {
    exec {
      commandLine 'rm', '-r', 'tmp_gh-pages'
    }
  }
}
